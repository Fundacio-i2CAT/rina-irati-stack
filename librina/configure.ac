#
# configure.ac
#
# Written by: Francesco Salvestrini <f DOT salvestrini AT nextworks DOT it>
#

AC_INIT([RINA Libraries],
        m4_esyscmd([build-aux/git-version-gen .tarball-version]),
        [f.salvestrini@nextworks.it],
        [librina],
        [http://irati.eu])

AC_PREREQ([2.68])

AC_CONFIG_HEADERS([src/config.h])
AC_CONFIG_SRCDIR([src/librina.cc])
AC_CONFIG_AUX_DIR([build-aux])
AC_CONFIG_MACRO_DIR([m4])

AC_CANONICAL_BUILD
AC_CANONICAL_HOST
AC_CANONICAL_TARGET

AM_INIT_AUTOMAKE([1.11.1 tar-pax foreign -Wall])
#AM_INIT_AUTOMAKE([1.11.1 tar-pax foreign std-options -Wall])

AC_PROG_SED
AC_PROG_GREP
AC_PROG_MKDIR_P
AC_PROG_LN_S

AC_PATH_PROG([FMT],  [fmt],  [/bin/false])
AC_PATH_PROG([SORT], [sort], [/bin/false])
AC_PATH_PROG([UNIQ], [uniq], [/bin/false])
AC_PATH_PROG([GIT],  [git],  [/bin/false])

AC_PROG_CXXCPP
AC_PROG_CXX
AC_PROG_CXX_C_O

CPPFLAGS_EXTRA=""
AX_CHECK_COMPILE_FLAG([ -Wall -Werror ], [
    CPPFLAGS_EXTRA="$CPPFLAGS_EXTRA -Wall -Werror"
])
AX_CHECK_COMPILE_FLAG([ -Wformat ], [
    CPPFLAGS_EXTRA="$CPPFLAGS_EXTRA -Wformat"
])
AC_SUBST(CPPFLAGS_EXTRA, $CPPFLAGS_EXTRA)

LT_INIT

#AX_HAVE_SYSCALL([ipc_create],,[
#    AC_MSG_ERROR([Your system lacks of ipc_create syscall])])
#AX_HAVE_SYSCALL([ipc_configure],,[
#    AC_MSG_ERROR([Your system lacks of ipc_configure syscall])])
#AX_HAVE_SYSCALL([ipc_destroy],,[
#    AC_MSG_ERROR([Your system lacks of ipc_destroy syscall])])
#AX_HAVE_SYSCALL([connection_create],,[
#    AC_MSG_ERROR([Your system lacks of connection_create syscall])])
#AX_HAVE_SYSCALL([connection_destroy],,[
#    AC_MSG_ERROR([Your system lacks of connection_destroy syscall])])
#AX_HAVE_SYSCALL([connection_update],,[
#    AC_MSG_ERROR([Your system lacks of connection_update syscall])])
#AX_HAVE_SYSCALL([sdu_read],,[
#    AC_MSG_ERROR([Your system lacks of sdu_read syscall])])
#AX_HAVE_SYSCALL([sdu_write],,[
#    AC_MSG_ERROR([Your system lacks of sdu_write syscall])])

PKG_PROG_PKG_CONFIG([0.26])

PKG_CHECK_MODULES(LIBNL3, [libnl-3.0 >= 3.1],, [
    AC_MSG_ERROR([Your system lacks of libnl-3 support])
])

PKG_CHECK_MODULES(LIBNLGENL3, [libnl-genl-3.0 >= 3.1],, [
    AC_MSG_ERROR([Your system lacks of libnl-genl-3 support])
])

AC_CHECK_LIB([pthread], [pthread_create],,[
    AC_MSG_ERROR([Your system lacks of libpthread support])
])

build_bindings=no
build_bindings_java=no
AX_SELECTOR_ENABLE([java-bindings],[
    build_bindings_java=yes
],[
    build_bindings_java=no
])
AS_IF([test "$build_bindings_java" = "yes"],[
    build_bindings=yes
])

AS_IF([test "$build_bindings" = "yes"],[
    AX_PKG_SWIG([2.0], [
        #
        # SWIG version > 2.0.4 && < 2.0.8 have a bug preventing correct
        # bindings generation
        #
    
        SWIG_VERSION=`$SWIG -version 2>&1 | grep 'SWIG Version' | sed 's/.*\([0-9][0-9]*\.[0-9][0-9]*\.[0-9][0-9]*\).*/\1/g'`
        AX_COMPARE_VERSION($SWIG_VERSION, le, [2.0.4],,[
            AX_COMPARE_VERSION($SWIG_VERSION, ge, [2.0.8],,[
                AC_MSG_ERROR([SWIG versions in 2.0.4, ..., 2.0.8 range will not compile])
            ])
        ])
    ], [
        AC_MSG_ERROR([Your system lacks of SWIG (>2.0) support, bindings generation is not possible])
    ])
])

AS_IF([test "$build_bindings_java" = "yes"],[
    AX_PROG_JAVAC
    AX_JNI_INCLUDE_DIR

    # FIXME: Ugly as hell ...
    JNI_CPPFLAGS=""
    for JNI_INCLUDE_DIR in $JNI_INCLUDE_DIRS ;
    do
            JNI_CPPFLAGS="$JNI_CPPFLAGS -I$JNI_INCLUDE_DIR"
    done
    AC_SUBST(JNI_CPPFLAGS, $JNI_CPPFLAGS)
    AX_PROG_JAR
])
AM_CONDITIONAL(BUILD_BINDINGS_JAVA, [test $build_bindings_java = yes])

# FIXME: Maven support should be enabled if Java bindings are enabled, to avoid
#        (unneeded) extra checks ...
AC_PATH_PROG([MVN],[mvn],[])
AM_CONDITIONAL(BUILD_MAVEN_SUPPORT, [test x"$MVN" != x""])

DX_DOXYGEN_FEATURE(ON)
DX_DOT_FEATURE(ON)
DX_HTML_FEATURE(ON)
DX_CHM_FEATURE(OFF)
DX_CHI_FEATURE(OFF)
DX_MAN_FEATURE(ON)
DX_RTF_FEATURE(OFF)
DX_XML_FEATURE(OFF)
DX_PDF_FEATURE(ON)
DX_PS_FEATURE(OFF)
DX_INIT_DOXYGEN($PACKAGE_NAME, [doxygen.cfg], [doxygen])

AC_CONFIG_FILES([
    Makefile

    include/Makefile
    src/Makefile

    wrap/Makefile
    wrap/java/Makefile

    test/Makefile

    doc/Makefile
])

AC_OUTPUT
